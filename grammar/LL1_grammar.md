
PROGRAM                 ->      SUB_PROGRAM_1 PROGRAM | ε

SUB_PROGRAM_1           ->      TYPE IDENTIFIER SUB_PROGRAM_2 
SUB_PROGRAM_2           ->      FUNCTION_DECL | VARIABLE_DECL

FUNCTION_DECL           ->      PARAMETER_LIST COMPOUND_STMT

PARAMETER_LIST          ->      ( PARAMETER_LIST_1 )
PARAMETER_LIST_1        ->      PARAMETERS | ε
PARAMETERS              ->      PARAMETER_DECL PARAMETER_LOOP
PARAMETER_LOOP          ->      , PARAMETER_DECL PARAMETER_LOOP | ε
PARAMETER_DECL          ->      TYPE IDENTIFIER DECLARATOR

VARIABLE_DECL           ->      DECLARATOR_LIST ;

DECLARATOR_LIST         ->      INIT_DECLARATOR DECLARATOR_LOOP
DECLARATOR_LOOP         ->      , INIT_DECLARATOR DECLARATOR_LOOP | ε

INIT_DECLARATOR         ->      DECLARATOR VARIABLE_DECL_1

DECLARATOR              ->      [ DECLARATOR_1 ] | ε
DECLARATOR_1            ->      int_literal | ε

VARIABLE_DECL_1         ->      = INITIALIZER | ε
INITIALIZER             ->      EXPR | { EXPR LIST_EXPR }
LIST_EXPR               ->      , EXPR LIST_EXPR | ε

COMPOUND_STMT           ->      { COMPOUND_STMT_1 COMPOUND_STMT_2 }
COMPOUND_STMT_1         ->      TYPE IDENTIFIER VARIABLE_DECL COMPOUND_STMT_1 | ε
COMPOUND_STMT_2         ->      STATEMENT COMPOUND_STMT_2 | ε

STATEMENT               ->      IF_STMT | FOR_STMT | WHILE_STMT | BREAK_STMT | CONTINUE_STMT | RETURN_STMT | EXPR_STMT

IF_STMT                 ->      if ( EXPR ) COMPOUND_STMT ELSE_STMT
ELSE_STMT               ->      else COMPOUND_STMT | ε
FOR_STMT                ->      for ( EXPR_OR_EPSILON ; EXPR_OR_EPSILON ; EXPR_OR_EPSILON ) COMPOUND_STMT
WHILE_STMT              ->      while ( EXPR ) COMPOUND_STMT
BREAK_STMT              ->      break ;
CONTINUE_STMT           ->      continue ;
RETURN_STMT             ->      return EXPR_OR_EPSILON ;
EXPR_STMT               ->      EXPR_OR_EPSILON ;
EXPR_OR_EPSILON         ->      EXPR | ε

EXPR                    ->      ASSIGNMENT_EXPR
ASSIGNMENT_EXPR         ->      CONDITION_OR_EXPR ASSIGNMENT_EXPR_1
ASSIGNMENT_EXPR_1       ->      = CONDITION_OR_EXPR ASSIGNMENT_EXPR_1 | ε

CONDITION_OR_EXPR       ->      CONDITION_AND_EXPR CONDITION_OR_EXPR_1
CONDITION_OR_EXPR_1     ->      double_vertical_bar CONDITION_AND_EXPR CONDITION_OR_EXPR_1 | ε

CONDITION_AND_EXPR      ->      EQUALITY_EXPR CONDITION_AND_EXPR_1
CONDITION_AND_EXPR_1    ->      && EQUALITY_EXPR CONDITION_AND_EXPR_1 | ε

EQUALITY_EXPR           ->      RELATIONAL_EXPR EQUALITY_EXPR_1
EQUALITY_EXPR_1         ->      == RELATIONAL_EXPR EQUALITY_EXPR_1 | != RELATIONAL_EXPR EQUALITY_EXPR_1 | ε

RELATIONAL_EXPR         ->      ADDITIVE_EXPR RELATIONAL_EXPR_1
RELATIONAL_EXPR_1       ->      < ADDITIVE_EXPR RELATIONAL_EXPR_1 | <= ADDITIVE_EXPR RELATIONAL_EXPR_1 | > ADDITIVE_EXPR RELATIONAL_EXPR_1 | >= ADDITIVE_EXPR RELATIONAL_EXPR_1 | ε

ADDITIVE_EXPR           ->      MULTIPLICATIVE_EXPR ADDITIVE_EXPR_1
ADDITIVE_EXPR_1         ->      + MULTIPLICATIVE_EXPR ADDITIVE_EXPR_1 | - MULTIPLICATIVE_EXPR ADDITIVE_EXPR_1 | ε

MULTIPLICATIVE_EXPR     ->      UNARY_EXPR MULTIPLICATIVE_EXPR_1
MULTIPLICATIVE_EXPR_1   ->      * UNARY_EXPR MULTIPLICATIVE_EXPR_1 | / UNARY_EXPR MULTIPLICATIVE_EXPR_1 | ε

UNARY_EXPR              ->      PRIMARY_EXPR UNARY_EXPR_1
UNARY_EXPR_1            ->      ! UNARY_EXPR_1 | ε

PRIMARY_EXPR            ->      IDENTIFIER PRIMARY_EXPR_1 | ( EXPR ) | int_literal | float_literal | bool_literal | string_literal
PRIMARY_EXPR_1          ->      ARG_LIST | [ EXPR ] | ε
ARG_LIST                ->      ( ARG_LIST_1 )
ARG_LIST_1              ->      ARG ARG_LIST_LOOP | ε
ARG_LIST_LOOP           ->      , ARG ARG_LIST_LOOP | ε
ARG                     ->      EXPR

TYPE                    ->      void | boolean | int | float
IDENTIFIER              ->      id
